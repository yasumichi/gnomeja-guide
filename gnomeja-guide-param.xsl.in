<?xml version='1.0' encoding="utf-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version='1.0'>

<!-- Options used for documentclass -->
<xsl:param name="latex.style">docbook-gnomeja-guide</xsl:param>
<xsl:param name="latex.class.book">dbkgnomeja</xsl:param>
<xsl:param name="latex.class.options">@@PAPERSIZE@@,10pt</xsl:param><!-- a4paper b5paper -->

<!-- Requires Japanese fonts -->
<xsl:param name="xetex.font">
  <xsl:text>\setjamainfont{@@JAMAINFONT@@}&#10;</xsl:text>
  <xsl:text>\setjasansfont{@@JASANSFONT@@}&#10;</xsl:text>
  <xsl:text>\setjamonofont{@@JAMONOFONT@@}&#10;</xsl:text>
</xsl:param>

<!-- Override encode.before.style in lang.xsl -->
<xsl:template name="encode.before.style">
  <xsl:param name="lang"/>
  <xsl:variable name="use-unicode">
    <xsl:call-template name="lang-in-unicode">
      <xsl:with-param name="lang" select="$lang"/>
    </xsl:call-template>
  </xsl:variable>

  <!-- XeTeX preamble to handle fonts -->
  <xsl:text>\usepackage{zxjatype}&#10;</xsl:text>
  <xsl:text>\usepackage{xltxtra}&#10;</xsl:text>
  <xsl:value-of select="$xetex.font"/>
</xsl:template>

<!-- Override figure.* in admon.xsl -->
<xsl:param name="figure.note">note</xsl:param>
<xsl:param name="figure.tip">tip</xsl:param>
<xsl:param name="figure.important">important</xsl:param>
<xsl:param name="figure.warning">warning</xsl:param>
<xsl:param name="figure.caution">caution</xsl:param>

<!-- Override procedure/title, step/title in procedure.xsl -->
<xsl:template match="procedure/title">
  <xsl:text>{\jafamily{rm}\scshape </xsl:text>
  <!-- <xsl:text>\paragraph{</xsl:text> -->
  <xsl:apply-templates/>
  <xsl:text>}&#10;</xsl:text>
  <xsl:call-template name="label.id">
    <xsl:with-param name="object" select=".."/>
  </xsl:call-template>
  <!-- Ask to latex to let the title with its list -->
  <xsl:text>\nopagebreak&#10;</xsl:text>
</xsl:template>

<xsl:template match="step/title">
  <xsl:text>{\jafamily{rm}\scshape </xsl:text>
  <!-- <xsl:text>\paragraph{</xsl:text> -->
  <xsl:apply-templates/>
  <xsl:text>}&#10;&#10;</xsl:text>
</xsl:template>

<!-- Override appendix in appendix.xsl -->
<xsl:template match="appendix">
  <xsl:if test="not (preceding-sibling::appendix)">
    <xsl:text>% ---------------------&#10;</xsl:text>
    <xsl:text>% Appendixes start here&#10;</xsl:text>
    <xsl:text>% ---------------------&#10;</xsl:text>
    <xsl:text>\appendix&#10;</xsl:text>
  </xsl:if>
  <xsl:call-template name="makeheading">
    <!-- raise to the highest existing book section level (part or chapter) -->
    <xsl:with-param name="level">
      <xsl:choose>
      <xsl:when test="preceding-sibling::part or
                      following-sibling::part">-1</xsl:when>
      <xsl:when test="parent::book or parent::part">0</xsl:when>
      <xsl:otherwise>1</xsl:otherwise>
      </xsl:choose>
    </xsl:with-param>
  </xsl:call-template>

  <xsl:apply-templates/>

  <xsl:if test="not (following-sibling::appendix)">
    <xsl:text>&#10;</xsl:text>
  </xsl:if>
</xsl:template>

</xsl:stylesheet>
